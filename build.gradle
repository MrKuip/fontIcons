import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardCopyOption

plugins {
  id 'java-library'
  id 'eclipse'
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = "org.kku"
version = "1.0.1"

defaultTasks 'jar'

apply from: 'build-eclipse.gradle'

repositories {
  mavenCentral()
}

javafx {
    version = "24"
    modules = [ 'javafx.controls' ]
}

java {
  withJavadocJar()
  withSourcesJar()
  sourceCompatibility = JavaVersion.VERSION_23
  targetCompatibility = JavaVersion.VERSION_23
}

dependencies {
  implementation 'com.ibm.icu:icu4j:74.2'
  implementation 'com.ibm.icu:icu4j-charset:74.2'
}

task generateFontDictionary(type: JavaExec) {
  getMainClass().set('org.kku.fonticons.generate.GenerateTtfDictionaries')
  args "src/main/resources/module-resources"
  classpath = sourceSets.main.runtimeClasspath

  def ttfFiles = fileTree("src/main/resources/module-resources/font") {
    include "**/*.ttf" 
  }

  inputs.files ttfFiles
  outputs.files ttfFiles.collect { file -> 
    file.absolutePath.replace(".ttf", ".properties");
  }
}

application {
  mainClass.set('org.kku.fonticons.sample.ImageViewExample');
  mainModule.set('org.kku.fontIcons');
}

tasks.register("downloadMaterialDesignIcons")  {
    def url = new URL('https://github.com/Templarian/MaterialDesign-Webfont/raw/master/fonts/materialdesignicons-webfont.ttf')
    def outputFile = file("$projectDir/src/main/resources/module-resources/font/materialdesignicons-webfont.ttf")

    inputs.property("url", url);
    outputs.file(outputFile);

    doLast {
        if (!outputFile.parentFile.exists()) {
            outputFile.parentFile.mkdirs()
        }

        // Use Streams to copy the URL content to the file
        url.withInputStream { inputStream ->
            Files.copy(inputStream, outputFile.toPath(), StandardCopyOption.REPLACE_EXISTING)
        }

        println "Downloaded materialdesignicons-webfont.ttf to ${outputFile.absolutePath}"
    }
}

tasks.named("processResources") {
    dependsOn("downloadMaterialDesignIcons")
}

jar.dependsOn downloadMaterialDesignIcons
jar.dependsOn generateFontDictionary
sourcesJar.dependsOn generateFontDictionary

tasks.javadoc {
  exclude('**/com/google/**')
  failOnError = false 
}

tasks.sourcesJar {
  exclude('**/com/google/**')
}

tasks.jar {
  exclude('**/com/google/**')
  manifest {
   attributes 'Implementation-Title': 'FontIcons (material design)',
              'Implementation-Version': version
  }
}

