import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardCopyOption

plugins {
  id 'java-library'
  id 'eclipse'
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0'
}

defaultTasks 'fontIconsjar'

apply from: 'build-eclipse.gradle'

repositories {
  mavenCentral()
}

javafx {
    version = "22.0.1"
    modules = [ 'javafx.controls' ]
}

dependencies {
  implementation 'com.ibm.icu:icu4j:74.2'
  implementation 'com.ibm.icu:icu4j-charset:74.2'
}

task generateFontDictionary(type: JavaExec) {
  getMainClass().set('org.kku.fonticons.generate.GenerateTtfDictionaries')
  args "src/main/resources"
  classpath = sourceSets.main.runtimeClasspath

  def ttfFiles = fileTree("src/main/resources/font") {
    include "**/*.ttf" 
  }

  inputs.files ttfFiles
  outputs.files ttfFiles.collect { file -> 
    file.absolutePath.replace(".ttf", ".properties");
  }
}

                
task fontIconsJar(type: Jar) {
  archiveBaseName = "fontIcons"
  manifest {
    attributes 'Implementation-Title': 'FontIcons (material design)',
               'Implementation-Version': version
  }
  includeEmptyDirs = false
  from sourceSets.main.output
  include "**/fonticons/ui/**/*.class", "**/fonticons/util/**/*.class", "font/**", "**/fonticons/sample/**/*.class", "module-info.class"
}

application {
  mainClass.set('org.kku.fonticons.sample.ImageViewExample');
  mainModule.set('org.kku.fontIcons');
}

tasks.register("downloadMaterialDesignIcons")  {
    def url = new URL('https://github.com/Templarian/MaterialDesign-Webfont/raw/master/fonts/materialdesignicons-webfont.ttf')
    def outputFile = file("$projectDir/src/main/resources/font/materialdesignicons-webfont.ttf")

    inputs.property("url", url);
    outputs.file(outputFile);

    doLast {
        if (!outputFile.parentFile.exists()) {
            outputFile.parentFile.mkdirs()
        }

        // Use Streams to copy the URL content to the file
        url.withInputStream { inputStream ->
            Files.copy(inputStream, outputFile.toPath(), StandardCopyOption.REPLACE_EXISTING)
        }

        println "Downloaded materialdesignicons-webfont.ttf to ${outputFile.absolutePath}"
    }
}

tasks.named("processResources") {
    dependsOn("downloadMaterialDesignIcons")
}

jar.dependsOn downloadMaterialDesignIcons
jar.dependsOn generateFontDictionary
fontIconsJar.dependsOn jar

